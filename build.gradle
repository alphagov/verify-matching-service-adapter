apply plugin: "java"
apply plugin: 'application'

ext {
    opensaml = '3.4.6'
    build_number="$version"
}

group = "uk.gov.ida"
project.version = "$gradle.ext.version_number-$build_number"

def dependencyVersions = [
        opensaml:"$opensaml",
        dropwizard: '2.1.2',
        ida_utils: '412',
        ida_test_utils: '67',
        dev_pki: '2.0.0-46',
        saml_libs_version: "$opensaml-275",
        glassfish_version:'2.6.1',
        glassfish_jersey_version:'2.34'
]

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
        mavenCentral()
    }
    else {
        maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
    }
}

configurations {
    dropwizard
    saml
    ida
    ida_test
}

configurations.all {
    exclude group:"javax.validation", module:"validation-api"
    resolutionStrategy {
        force "ch.qos.logback:logback-classic:1.2.9"
        force "ch.qos.logback:logback-access:1.2.9"
        force "org.apache.santuario:xmlsec:2.1.8"
        force "commons-collections:commons-collections:3.2.2"
    }
}

dependencies {
    dropwizard "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
        "io.dropwizard:dropwizard-util:$dependencyVersions.dropwizard",
        'io.dropwizard.metrics:metrics-healthchecks:3.1.2',
        "io.dropwizard:dropwizard-jersey:$dependencyVersions.dropwizard",
        'io.dropwizard.metrics:metrics-annotation:4.2.15',
        "io.dropwizard:dropwizard-client:$dependencyVersions.dropwizard",
        "io.dropwizard:dropwizard-metrics-graphite:$dependencyVersions.dropwizard",
        "org.glassfish.hk2:guice-bridge:$dependencyVersions.glassfish_version",
        "org.glassfish.jersey.inject:jersey-hk2:$dependencyVersions.glassfish_jersey_version",
        "org.glassfish.jersey.media:jersey-media-jaxb:$dependencyVersions.glassfish_jersey_version",
        "com.google.inject.extensions:guice-servlet:5.0.1"

    saml "org.opensaml:opensaml-core:$dependencyVersions.opensaml",
        "org.opensaml:opensaml-security-api:$dependencyVersions.opensaml",
        "org.opensaml:opensaml-saml-api:$dependencyVersions.opensaml",
        "org.opensaml:opensaml-xmlsec-api:$dependencyVersions.opensaml",
        "org.opensaml:opensaml-saml-impl:$dependencyVersions.opensaml",
        "org.opensaml:opensaml-xmlsec-impl:$dependencyVersions.opensaml",
        'net.shibboleth.utilities:java-support:7.2.0'

    ida "uk.gov.ida:saml-lib:$dependencyVersions.saml_libs_version",
        "uk.gov.ida:rest-utils:2.0.0-$dependencyVersions.ida_utils",
        "uk.gov.ida:security-utils:2.0.0-$dependencyVersions.ida_utils",
        "uk.gov.ida:common-utils:2.0.0-$dependencyVersions.ida_utils"

    implementation configurations.dropwizard,
        configurations.ida,
        'javax.inject:javax.inject:1',
        'com.fasterxml.jackson.core:jackson-annotations:2.13.4',
        'com.fasterxml.jackson.core:jackson-databind:2.13.4',
        'com.google.inject:guice:5.1.0',
        'joda-time:joda-time:2.9',
        'org.slf4j:slf4j-api:1.7.21',
        'javax.ws.rs:javax.ws.rs-api:2.0.1',
        'commons-codec:commons-codec:1.15',
        'commons-lang:commons-lang:2.6',
        'org.apache.ws.commons:ws-commons-util:1.0.1',
        'com.google.code.findbugs:annotations:3.0.0',
        'xml-apis:xml-apis:1.0.b2'
        configurations.saml   {
            exclude group: 'uk.gov.ida', module: 'dropwizard-saml'
        }

    runtime "io.dropwizard:dropwizard-metrics-graphite:$dependencyVersions.dropwizard"

    ida_test "uk.gov.ida:saml-test:$dependencyVersions.saml_libs_version",
        "uk.gov.ida:verify-dev-pki:$dependencyVersions.dev_pki"

    testImplementation configurations.ida_test,
        "org.junit.jupiter:junit-jupiter-api:5.9.1",
        "org.mockito:mockito-junit-jupiter:3.8.0",
        "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard",
        "io.dropwizard:dropwizard-jackson:$dependencyVersions.dropwizard",
        'org.assertj:assertj-core:3.23.1',
        'org.mockito:mockito-core:4.8.0',
        "uk.gov.ida:common-test-utils:2.0.0-$dependencyVersions.ida_test_utils"

    testImplementation ("com.github.tomakehurst:wiremock:2.16.0") { transitive = false }



    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

sourceSets {
    java9 {
        if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
            java {
                srcDir 'src/main/java9'
            }
        }
    }
    java10 {
        if(JavaVersion.current() >= JavaVersion.VERSION_1_10) {
            java {
                srcDir 'src/main/java10'
            }
        }
    }
    intTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
            srcDir 'configuration'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

compileJava9Java {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        options.compilerArgs.addAll(['--release', '9'])
    }
}

compileJava10Java {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_10) {
        options.compilerArgs.addAll(['--release', '10'])
    }
}

ext.mainclass = 'uk.gov.ida.matchingserviceadapter.MatchingServiceAdapterApplication'
mainClassName = ext.mainclass

task copyToLib(dependsOn: jar, type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

task copyTestToolZip(dependsOn: 'verify-matching-service-test-tool:distZip', type: Copy) {
    from("verify-matching-service-test-tool/build/distributions/verify-matching-service-test-tool-*.zip")
    into(distsDir)
}

task zip(dependsOn: [copyToLib, copyTestToolZip], type: Zip) {
    from("$buildDir/output").into('./')
    from('RELEASE_NOTES.md', 'USAGE_README.md', 'prod-config.yml', 'test-config.yml').into('./')
    rename('USAGE_README\\.md', 'README\\.md')
}

task intTest(type: Test) {
    String maxParallelForksEnvVarValue = 1
    println "Overriding maxParallelForks value (value: \'${maxParallelForksEnvVarValue}\')"
    maxParallelForks = Integer.parseInt("${maxParallelForksEnvVarValue}")
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath += sourceSets.intTest.runtimeClasspath
}

tasks.withType(Test) {
    useJUnitPlatform()
}

run {
    args = ["server", "configuration/verify-matching-service-adapter.yml"]
}

task(outputDependencies) doLast {
    println "ida_utils=$dependencyVersions.ida_utils"
}

distributions {
    paas {
        contents {
            from 'configuration/example-lms-msa.yml',
                 'src/main/resources/test_metadata_truststore.ts'

            with distributions.main.contents
        }
    }
}


tasks.check.dependsOn(intTest)

distZip {
    into(project.name + '/truststores') {
        from 'src/main/resources'
        include '*_metadata_truststore.ts'
    }
    into(project.name) {
        from 'configuration'
        include 'test-rp-msa.yml'
    }

    def versionedPath = archivePath
    version = null
    doLast {
        archivePath.renameTo(versionedPath)
    }
}

jar {
    def manifestClasspath = configurations.runtime.collect { 'lib/' + it.getName() }.join(' ')
    def buildNumber = System.getenv('BUILD_NUMBER')
    def gitCommit = System.getenv('GIT_COMMIT')

    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    into('META-INF/versions/10') {
        from sourceSets.java10.output
    }
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Class-Path': manifestClasspath,
                'Build-Number': buildNumber != null ? buildNumber : '',
                'Version-Number': project.version,
                'Git-Commit': gitCommit != null ? gitCommit : '',
                'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm'Z'")
        )
        if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
            attributes.put("Multi-Release", "true")
        }
    }

    archiveName = project.name + '.jar'
    destinationDir = file("$buildDir/output")
}
